module Bus_Control_Logic (
    input   wire           clock,
    input   wire           reset,

    input   wire           chip_select_n,
    input   wire           read_enable_n,
    input   wire           write_enable_n,
    input   wire           address,
    input   wire   [7:0]   data_bus_in,

    // Internal Bus
    output  wire   [7:0]   internal_data_bus,
    output  wire           write_initial_command_word_1,
    output  wire           write_initial_command_word_2_4,
    output  wire           write_operation_control_word_1,
    output  wire           write_operation_control_word_2,
    output  wire           write_operation_control_word_3,
    output  wire           read
);

    //
    // Internal Signals
    //
    reg   [7:0] internal_data_bus_reg;
    reg   prev_write_enable_n;
    wire   write_flag;
    reg   stable_address;

    //
    // Write Control
    //
    always @(negedge clock or posedge reset) begin
        if (reset)
            internal_data_bus_reg <= 8'b00000000;
        else if (~write_enable_n & ~chip_select_n)
            internal_data_bus_reg <= data_bus_in;
    end

    always @(negedge clock or posedge reset) begin
        if (reset)
            prev_write_enable_n <= 1'b1;
        else if (chip_select_n)
            prev_write_enable_n <= 1'b1;
        else
            prev_write_enable_n <= write_enable_n;
    end
    assign write_flag = ~prev_write_enable_n & write_enable_n;

    always @(negedge clock or posedge reset) begin
        if (reset)
            stable_address <= 1'b0;
        else
            stable_address <= address;
    end

    // Generate write request flags
    assign internal_data_bus = internal_data_bus_reg;
    assign write_initial_command_word_1   = write_flag & ~stable_address & internal_data_bus[4];
    assign write_initial_command_word_2_4 = write_flag & stable_address;
    assign write_operation_control_word_1 = write_flag & stable_address;
    assign write_operation_control_word_2 = write_flag & ~stable_address & ~internal_data_bus[4] & ~internal_data_bus[3];
    assign write_operation_control_word_3 = write_flag & ~stable_address & ~internal_data_bus[4] & internal_data_bus[3];

    //
    // Read Control
    //
    assign read = ~read_enable_n  & ~chip_select_n;

endmodule
